dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(unix)
AM_CONFIG_HEADER(include/config.h)

dnl Setup library CURRENT, REVISION and AGE
LIBCUR=0
LIBREV=7
LIBAGE=0
AM_INIT_AUTOMAKE(libsidplay,2.$LIBCUR.$LIBREV)
dnl libtool-style version-info number
LIBVERSION=$LIBCUR:$LIBREV:$LIBAGE
AC_SUBST(LIBVERSION)

AC_CANONICAL_HOST

dnl Use C++ for tests.
AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB

dnl Initialize libtool.
AC_LIBTOOL_WIN32_DLL
CONFIG_LIBTOOL

dnl Check for hardsid card.
CHECK_HARDSID
if test "$sid_have_hardsid" = yes; then
    AC_DEFINE(HAVE_HARDSID)
fi

dnl Checks for libraries.
SID_PATH_LIBRESID

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
if test $ac_cv_c_bigendian = yes; then
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_BIGENDIAN)
else
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_LITTLEENDIAN)
fi

AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short int, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long int, 4)

SID_SUBST(SID_SIZEOF_CHAR,$ac_cv_sizeof_char)
SID_SUBST(SID_SIZEOF_SHORT_INT,$ac_cv_sizeof_short_int)
SID_SUBST(SID_SIZEOF_INT,$ac_cv_sizeof_int)
SID_SUBST(SID_SIZEOF_LONG_INT,$ac_cv_sizeof_long_int)

dnl Check whether the compiler has bool.
CHECK_BOOL
if test "$test_cv_have_bool" = yes; then
    SID_SUBST_DEF(SID_HAVE_BOOL)
else
    SID_SUBST_UNDEF(SID_HAVE_BOOL)
fi

dnl Check for nothrow allocator.
CHECK_EXCEPTIONS

dnl Check whether ios::binary is ios::bin.
CHECK_IOS_BIN

dnl Checks for library functions.
AC_HEADER_STDC

AC_CHECK_FUNC(strncasecmp,[AC_DEFINE(HAVE_STRNCASECMP,)],[])
AC_CHECK_FUNC(strcasecmp,[AC_DEFINE(HAVE_STRCASECMP,)],[])
AC_CHECK_HEADER(strstrea.h,[AC_DEFINE(HAVE_STRSTREA_H,)],[])

AC_SYS_LONG_FILE_NAMES

dnl Check for AC99 headers
AC_CHECK_HEADERS(stdint.h stdbool.h)
if test "$ac_cv_header_stdint_h" = yes; then
    SID_SUBST_DEF(SID_HAVE_STDINT_H)
else
    SID_SUBST_UNDEF(SID_HAVE_STDINT_H)
fi
if test "$ac_cv_header_stdbool_h" = yes; then
    SID_SUBST_DEF(SID_HAVE_STDBOOL_H)
else
    SID_SUBST_UNDEF(SID_HAVE_STDBOOL_H)
fi

dnl Add absolute directory for header files
CXXFLAGS="$CXXFLAGS -I\$(top_srcdir)/include/sidplay \$(RESID_INCLUDES) -Wall"

AC_OUTPUT(
libsidplay.spec \
Makefile \
include/Makefile \
include/sidplay/Makefile \
include/sidplay/sidconfig.h \
src/Makefile \
src/mos6510/Makefile \
src/mos6526/Makefile \
src/mos656x/Makefile \
src/mos6581/Makefile \
src/sidtune/Makefile \
src/xsid/Makefile
)
