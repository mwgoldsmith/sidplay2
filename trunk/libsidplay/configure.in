dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(scripts)
AM_CONFIG_HEADER(config.h)

LIBVERSION_CUR=0
LIBVERSION_REV=7
LIBVERSION_AGE=0

AM_INIT_AUTOMAKE(libsidplay,2.$LIBVERSION_CUR.$LIBVERSION_REV)
LIBVERSION=$LIBVERSION_CUR:$LIBVERSION_REV:$LIBVERSION_AGE
AC_SUBST(LIBVERSION)dnl libtool-style version or release number

AC_CANONICAL_HOST

dnl Use C++ for tests.
AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
dnl Checks for libraries.

SID_PATH_LIBRESID

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
if test $ac_cv_c_bigendian = yes; then
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_BIGENDIAN)
else
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_LITTLEENDIAN)
fi

AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short int, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long int, 4)

SID_SUBST(SID_SIZEOF_CHAR,$ac_cv_sizeof_char)
SID_SUBST(SID_SIZEOF_SHORT_INT,$ac_cv_sizeof_short_int)
SID_SUBST(SID_SIZEOF_INT,$ac_cv_sizeof_int)
SID_SUBST(SID_SIZEOF_LONG_INT,$ac_cv_sizeof_long_int)

dnl Check whether the compiler has bool.
SID_CHECK_BOOL

dnl Check for nothrow allocator.
SID_CHECK_EXCEPTIONS

dnl Check whether ios::binary is ios::bin.
SID_CHECK_IOS_BIN

dnl Checks for library functions.
AC_HEADER_STDC

AC_CHECK_FUNC(strncasecmp,[SID_SUBST_DEF(SID_HAVE_STRNCASECMP)],
    [SID_SUBST_UNDEF(SID_HAVE_STRNCASECMP)])
AC_CHECK_FUNC(strcasecmp,[SID_SUBST_DEF(SID_HAVE_STRCASECMP)],
    [SID_SUBST_UNDEF(SID_HAVE_STRCASECMP)])

AC_CHECK_HEADER(strstrea.h,[SID_SUBST_DEF(SID_HAVE_STRSTREA_H)],
    [SID_SUBST_UNDEF(SID_HAVE_STRSTREA_H)])

dnl AC_CHECK_HEADER(strstrea.h,[AC_DEFINE(SID_HAVE_STRSTREA_H)],[])

AC_SYS_LONG_FILE_NAMES
if test $ac_cv_sys_long_file_names = yes; then
    SID_SUBST_DEF(SID_HAVE_LONG_FILE_NAMES)
else
    SID_SUBST_UNDEF(SID_HAVE_LONG_FILE_NAMES)
fi

dnl Checks what version of Unix we have and souncard support

AC_CHECK_HEADERS(sys/ioctl.h linux/soundcard.h machine/soundcard.h  \
  soundcard.h sys/audio.h sun/audioio.h sun/dbriio.h sys/audioio.h  \
  audio.h dmedia/audio.h)

sid_audiodrv_libadd=""
AC_MSG_CHECKING(which audio driver to install)
case "$host" in
  *linux*) AC_DEFINE(HAVE_OSS,1)
    sid_audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
  *freebsd*) AC_DEFINE(HAVE_OSS,1)
    sid_audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
  *hpux*) AC_DEFINE(HAVE_HPUX,1)
    AC_MSG_RESULT(hpux)
       ;;
  *sgi*) AC_DEFINE(HAVE_SGI,1)
    AC_MSG_RESULT(sgi)
       ;;
  *solaris*) AC_DEFINE(HAVE_SUNOS,1)
    sid_audiodrv_libadd="./audio/sunos/libsunos.a"
    AC_MSG_RESULT(sparc)
       ;;
  *sunos*) AC_DEFINE(HAVE_SUNOS,1)
    sid_audiodrv_libadd="./audio/sunos/libsunos.a"
    AC_MSG_RESULT(sparc)
       ;;
  *netbsd*) AC_DEFINE(HAVE_OSS,1)
    sid_audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
esac
AUDIODRV_LDADD="$sid_audiodrv_libadd"
AC_SUBST(AUDIODRV_LDADD)

dnl Initialize libtool.
SID_PROG_LIBTOOL

dnl Add absolute directory for header files
CXXFLAGS="$CXXFLAGS -I\$(top_srcdir)/include"

AC_OUTPUT(sidconfig.h Makefile include/Makefile \
src/Makefile \
src/audio/Makefile \
src/audio/oss/Makefile \
src/audio/sunos/Makefile \
src/audio/wav/Makefile \
src/docs/Makefile \
src/docs/en/Makefile \
src/mos6510/Makefile \
src/mos6581/Makefile \
src/sidtune/Makefile \
src/xsid/Makefile \
)

