dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(unix/scripts)
AM_CONFIG_HEADER(unix/config.h)

dnl Setup library CURRENT, REVISION and AGE
LIBCUR=0
LIBREV=6
LIBAGE=0
AM_INIT_AUTOMAKE(libsidplay,2.$LIBCUR.$LIBREV)
dnl libtool-style version-info number
LIBVERSION=$LIBCUR:$LIBREV:$LIBAGE
AC_SUBST(LIBVERSION)

AC_CANONICAL_HOST

dnl Use C++ for tests.
AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
dnl Checks for libraries.

SID_PATH_LIBRESID

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
if test $ac_cv_c_bigendian = yes; then
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_BIGENDIAN)
else
    AC_DEFINE(WORDS_LITTLEENDIAN)
    SID_SUBST(SID_WORDS_ENDIANESS,SID_WORDS_LITTLEENDIAN)
fi

AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short int, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long int, 4)

SID_SUBST(SID_SIZEOF_CHAR,$ac_cv_sizeof_char)
SID_SUBST(SID_SIZEOF_SHORT_INT,$ac_cv_sizeof_short_int)
SID_SUBST(SID_SIZEOF_INT,$ac_cv_sizeof_int)
SID_SUBST(SID_SIZEOF_LONG_INT,$ac_cv_sizeof_long_int)

dnl Check whether the compiler has bool.
CHECK_BOOL
if test "$test_cv_have_bool" = yes; then
    SID_SUBST_DEF(SID_HAVE_BOOL)
else
    SID_SUBST_UNDEF(SID_HAVE_BOOL)
fi

dnl Check for nothrow allocator.
CHECK_EXCEPTIONS

dnl Check whether ios::binary is ios::bin.
CHECK_IOS_BIN

dnl Checks for library functions.
AC_HEADER_STDC

AC_CHECK_FUNC(strncasecmp,[AC_DEFINE(HAVE_STRNCASECMP,)],[])
AC_CHECK_FUNC(strcasecmp,[AC_DEFINE(HAVE_STRCASECMP,)],[])
AC_CHECK_HEADER(strstrea.h,[AC_DEFINE(HAVE_STRSTREA_H,)],[])

AC_SYS_LONG_FILE_NAMES

dnl Checks what version of Unix we have and souncard support

AC_CHECK_HEADERS(sys/ioctl.h linux/soundcard.h machine/soundcard.h  \
  soundcard.h sys/audio.h sun/audioio.h sun/dbriio.h sys/audioio.h  \
  audio.h dmedia/audio.h)

dnl Check for AC99 headers
AC_CHECK_HEADERS(stdint.h stdbool.h)
if test "$ac_cv_header_stdint_h" = yes; then
    SID_SUBST_DEF(SID_HAVE_STDINT_H)
else
    SID_SUBST_UNDEF(SID_HAVE_STDINT_H)
fi
if test "$ac_cv_header_stdbool_h" = yes; then
    SID_SUBST_DEF(SID_HAVE_STDBOOL_H)
else
    SID_SUBST_UNDEF(SID_HAVE_STDBOOL_H)
fi


audiodrv_libadd=""
AC_MSG_CHECKING(which audio subsystem is available)
case "$host" in
  *linux*) AC_DEFINE(HAVE_OSS,)
    audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
  *freebsd*) AC_DEFINE(HAVE_OSS,)
    audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
  *hpux*) AC_DEFINE(HAVE_HPUX,)
    AC_MSG_RESULT(hpux)
       ;;
  *sgi*) AC_DEFINE(HAVE_SGI,)
    AC_MSG_RESULT(sgi)
       ;;
  *solaris*) AC_DEFINE(HAVE_SUNOS,)
    audiodrv_libadd="./audio/sunos/libsunos.a"
    AC_MSG_RESULT(sparc)
       ;;
  *sunos*) AC_DEFINE(HAVE_SUNOS,)
    audiodrv_libadd="./audio/sunos/libsunos.a"
    AC_MSG_RESULT(sparc)
       ;;
  *netbsd*) AC_DEFINE(HAVE_OSS,)
    audiodrv_libadd="./audio/oss/liboss.a"
    AC_MSG_RESULT(oss)
       ;;
esac
AUDIODRV_LDADD="$audiodrv_libadd"
AC_SUBST(AUDIODRV_LDADD)

dnl Initialize libtool.
AC_LIBTOOL_WIN32_DLL
CONFIG_LIBTOOL

dnl Add absolute directory for header files
CXXFLAGS="$CXXFLAGS -I\$(top_srcdir)/include -I\$(top_srcdir)/unix"

AC_OUTPUT(
Makefile \
include/Makefile \
unix/Makefile \
unix/sidconfig.h \
src/Makefile \
src/audio/Makefile \
src/audio/oss/Makefile \
src/audio/sunos/Makefile \
src/audio/wav/Makefile \
src/docs/Makefile \
src/docs/en/Makefile \
src/mos6510/Makefile \
src/mos6581/Makefile \
src/sidtune/Makefile \
src/xsid/Makefile
)
